#!/usr/bin/env coffee
fs = require 'fs'
path = require 'path'
{bold: b, italic: em, blue: bl} = require 'chalk'

HELPMESSAGE = """
#{b bl "xfce2json - 1.0.0"}
  Converts xfce4-terminal color scheme to (termcolors compatible) JSON

#{b "USAGE:"}
  #{em "Dispaly this message"}
  #{bl "$ xfce2json --help"}
  #{bl "$ xfce2json -h"}

  #{em "Output to a file"}
  #{bl "$ xfce2json <input> [output]"}

  #{em "Output to terminal(stdout)"}
  #{bl "$ xfce2json <input> -"}

#{b "Example with termcolors:"}
  Convert xfce4 colorscheme to termite colorscheme
  #{bl "$ xfce2json ./sometheme.theme - | termcolors -i json -o termite"}
"""

main = (argv) ->
  try
    [input, output] = argv
    xfpairs = fs.readFileSync path.resolve(input), 'utf8'
      .split '\n'
      .filter RegExp::test.bind /Foreground|Background|Palette/
      .map (ln) =>
        [key, val] = ln.toLowerCase().split '='
        "#{key.replace 'color', ''}": val
    {foreground, background, palette} = Object.assign {}, xfpairs...
    final = Object.assign {foreground, background}, palette.split ';'
  catch err
    if not (foreground? and background? and palette?) or err?
      console.error err or 'Invalid colorscheme file'
      process.exit 1
  finally if final?
    if output is '-'
      console.log JSON.stringify final, null, 4
    else
      fs.writeFileSync path.resolve(output), JSON.stringify(final, null, 4)
      console.log "Successfully output to file: '#{output}'"

help = ->
  console.log HELPMESSAGE

if module is require.main
  argv = process.argv.slice 2
  if '-h' in argv or '--help' in argv
    do help
    process.exit 0
  else if argv.length isnt 2
    console.error 'Not enough arguments'
    console.log 'Pass -h or --help for usage information'
    process.exit 1
  main argv

